start phase RootPhase {
    info = "Denotes the entry point of your compiler",

    actions {
        pass SPdoScanParse;
        print;
    }
};

traversal Print {
    uid = PRT
};

/*** AST ***/

enum MonOpType {
    prefix = MO,
    values {
        not, neg
    }
};

enum BinOpType {
    prefix = BO,
    values {
        add, sub, mul, div, mod, lt, le, gt, ge, eq, ne,
        and, or
    }
};

enum DeclarationType {
    prefix = TY,
    values {
        int, float, bool, void
    }
};

nodeset Decl = {FunDef};
nodeset Stmt = {Assign, Ifstatement, Ifelsestatement, Whilestatement, Dostatement, Forstatement, Returnstatement};
nodeset Expr = {Num, Float, Bool, Binop, Var, Monop, FunCall, TypeCast};

// Nodes you want to be able to link to from your symbol table.
nodeset Link = Expr | Stmt;

root node Program {
    children {
        Decls decls { constructor, mandatory }
    }
};

node Stmts {
    children {
        Stmt stmt { constructor, mandatory },
        Stmts next { constructor }
    }
};

node FunDef {
    children {
        FunHeader header { constructor, mandatory },
        FunBody body { constructor, mandatory }
    }
};

node FunHeader {
    children {
        Params params { constructor }
    },

    attributes {
        DeclarationType type { constructor },
        string name { constructor }
    }
};

node FunBody {
    children {
        VarDecs decl { constructor },
        Stmts stmts { constructor }
    }
};

node Decls {
    children {
        Decl decl { constructor, mandatory },
        Decls next { constructor }
    }
};

node Block {
    children {
        Stmts stmts { constructor }
    }
};

node Assign {
    children {
        VarLet let { constructor },
        Expr expr { constructor, mandatory }
    }
};

node VarDecs {
    children {
        VarDec dec { constructor, mandatory },
        VarDecs next { constructor }
    }
};

node VarDec {
    children {
        Expr expr { constructor }
    },

    attributes {
        DeclarationType type { constructor },
        string name { constructor }
    }
};

node Params {
    children {
        Param param { constructor, mandatory },
        Params params { constructor }
    }
};

node Param {
    attributes {
        DeclarationType type { constructor },
        string name { constructor, mandatory }
    }
};

node Ifstatement {
    children {
        Block block {constructor},
        Expr expr { constructor, mandatory}
    }
};

node Ifelsestatement {
    children {
        Block ifblock {constructor},
        Expr expr { constructor, mandatory},
        Block elseblock { constructor }
    }
};

node Whilestatement {
    children {
        Block block {constructor},
        Expr expr { constructor, mandatory}
    }
};

node Dostatement {
    children {
        Block block {constructor},
        Expr expr { constructor, mandatory}
    }
};

node Forstatement {
    children {
        Expr init { constructor, mandatory },

        Expr until { constructor, mandatory },
        Expr step { constructor },

        Block block { constructor, mandatory }
    },

    attributes {
        string variable { constructor, mandatory }
    }
};

node Returnstatement {
    children {
        Expr expr {constructor}
    }
};

node Binop {
    children {
        Expr left { constructor, mandatory },
        Expr right { constructor, mandatory }
    },

    attributes {
        BinOpType op { constructor }
    }
};

node Monop {
    children {
        Expr expr { constructor, mandatory }
    },

    attributes {
        MonOpType op { constructor }
    }
};

node FunCall {
    children {
        Exprs exprs { constructor }
    },

    attributes {
        string name { constructor }
    }
};

node Exprs {
    children {
        Expr expr { constructor, mandatory },
        Exprs next { constructor }
    }
};

node TypeCast {
    children {
        Expr expression { constructor, mandatory }
    },

    attributes {
        DeclarationType type { constructor }
    }
};

nodeset Vars {
    nodes = {VarLet, Var},

    attributes {
        string name,
        Link decl
    }
};

node VarLet {
    attributes {
        string name { constructor }  // Repeat from nodeset to add constructor
    }
};

node Var {
    attributes {
        string name { constructor }
    }
};

node Num {
    attributes {
        int val { constructor }
    }
};

node Float {
    attributes {
        float val { constructor }
    }
};

node Bool {
    attributes {
        bool val { constructor }
    }
};
