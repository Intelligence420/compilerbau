start phase RootPhase {
    info = "Denotes the entry point of your compiler",

    actions {
        pass SPdoScanParse;
        Print;
    }
};

traversal Print {
    uid = PRT
};

/*** AST ***/

enum MonOpType {
    prefix = MO,
    values {
        not, neg
    }
};

enum BinOpType {
    prefix = BO,
    values {
        add, sub, mul, div, mod, lt, le, gt, ge, eq, ne,
        and, or
    }
};

enum DeclarationType {
    prefix = TY,
    values {
        int, float, bool, void
    }
};

nodeset Decl = {FunDef, FunDec, GlobalDec, GlobalDef};
nodeset Stmt = {Assign, FunCallStmt, Ifstatement, Ifelsestatement, Whilestatement, Dostatement, Forstatement, Returnstatement};
nodeset Expr = {Num, Float, Bool, Binop, Var, Monop, FunCall, VarRef, TypeCast};

root node Program {
    children {
        Decls decls { constructor, mandatory }
    }
};

node Stmts {
    children {
        Stmt stmt { constructor, mandatory },
        Stmts next { constructor }
    }
};

node FunDef {
    children {
        FunHeader header { constructor, mandatory },
        FunBody body { constructor, mandatory }
    },

    attributes {
        bool export { constructor }
    }
};

node FunDec {
    children {
        FunHeader header { constructor, mandatory}
    }
};

node GlobalDec {
    children {
        Ids ids { constructor }
    },

    attributes {
        DeclarationType type { constructor },
        string name { constructor }
    }
};

node GlobalDef {
    children {
        VarDef dec { constructor, mandatory }
    },

    attributes {
        bool export { constructor }
    }
};

node LocalFundef {
    children {
        FunHeader header { constructor, mandatory },
        FunBody body { constructor, mandatory }
    }
};

node ArrExpr {
    children {
        Exprs exprs { constructor, mandatory }
    }
};

node FunHeader {
    children {
        Params params { constructor }
    },

    attributes {
        DeclarationType type { constructor },
        string name { constructor }
    }
};

node FunBody {
    children {
        VarDefs decl { constructor },
        FunDefs funs { constructor },
        Stmts stmts { constructor }
    }
};

node Decls {
    children {
        Decl decl { constructor, mandatory },
        Decls next { constructor }
    }
};

node Block {
    children {
        Stmts stmts { constructor }
    }
};

node Assign {
    children {
        Var let { constructor },
        Expr expr { constructor, mandatory }
    }
};

node VarDefs {
    children {
        VarDef dec { constructor, mandatory },
        VarDefs next { constructor }
    }
};

node FunDefs {
    children {
        FunDef fun { constructor, mandatory},
        FunDefs next { constructor }
    }
};

node VarDef {
    children {
        Expr expr { constructor },
        Exprs exprs { constructor }
    },

    attributes {
        DeclarationType type { constructor },
        string name { constructor }
    }
};

node Ids {
    children {
        Ids next { constructor }
    },

    attributes {
        string id { constructor, mandatory } 
    }
};

node Params {
    children {
        Param param { constructor, mandatory },
        Params params { constructor }
    }
};

node Param {
    children {
        Ids ids { constructor }
    },

    attributes {
        DeclarationType type { constructor },
        string name { constructor, mandatory }
    }
};

node Ifstatement {
    children {
        Block block {constructor},
        Expr expr { constructor, mandatory}
    }
};

node Ifelsestatement {
    children {
        Block ifblock {constructor},
        Expr expr { constructor, mandatory},
        Block elseblock { constructor }
    }
};

node Whilestatement {
    children {
        Block block {constructor},
        Expr expr { constructor, mandatory}
    }
};

node Dostatement {
    children {
        Block block {constructor},
        Expr expr { constructor, mandatory}
    }
};

node Forstatement {
    children {
        Expr init { constructor, mandatory },

        Expr until { constructor, mandatory },
        Expr step { constructor },

        Block block { constructor, mandatory }
    },

    attributes {
        string variable { constructor, mandatory }
    }
};

node Returnstatement {
    children {
        Expr expr {constructor}
    }
};

node FunCallStmt {
    children {
        FunCall call { constructor, mandatory }
    }
};

node VarRef {
    children {
        Var var { constructor, mandatory }
    }
};

node Binop {
    children {
        Expr left { constructor, mandatory },
        Expr right { constructor, mandatory }
    },

    attributes {
        BinOpType op { constructor }
    }
};

node Monop {
    children {
        Expr expr { constructor, mandatory }
    },

    attributes {
        MonOpType op { constructor }
    }
};

node FunCall {
    children {
        Exprs exprs { constructor }
    },

    attributes {
        string name { constructor }
    }
};

node Exprs {
    children {
        Expr expr { constructor, mandatory },
        Exprs next { constructor }
    }
};

node TypeCast {
    children {
        Expr expr { constructor, mandatory }
    },

    attributes {
        DeclarationType type { constructor }
    }
};  

node Var {
    attributes {
        string name { constructor }
    }
};

node Num {
    attributes {
        int val { constructor }
    }
};

node Float {
    attributes {
        float val { constructor }
    }
};

node Bool {
    attributes {
        bool val { constructor }
    }
};
